#!/etc/profiles/per-user/db/bin/zsh
set -euo pipefail

setopt EXTENDED_GLOB
setopt LOCAL_OPTIONS
setopt LOCAL_TRAPS
setopt ERR_EXIT

export LANG="${LANG:-en_GB.UTF-8}"

typeset -a changed_dhall=()

get_changed_dhall() {
  local CHANGED
  git status --porcelain \
    | awk '$0 ~ /^(A|M)/ && $2 ~ /\.dhall$/ {print $2}' \
    | { read -r -d "" CHANGED || : }

  changed_dhall+=("${(f)CHANGED}")

  # print -l -- "${(@)changed_dhall}" >&2
}

convert_dhall() {
  zparseopts -D -E -F -- \
    -test=OPT_test t=OPT_test

  local -a custom_tags=(
    "FindInMap"
    "Ref"
  )

  local -a custom_fns=(
    "And"
    "Base64"
    "Cidr"
    "Equals"
    "GetAZs"
    "GetAtt"
    "If"
    "ImportValue"
    "Join"
    "Not"
    "Or"
    "Select"
    "Split"
    "Sub"
    "fn"
  )

  local OUTPUT="${${1?Dhall input}:r}.yml"
  dhall-to-json \
    < =(git show :"$1") \
    | yj -r \
    | sed -E -e 's/('"${(j:|:)custom_tags}"'):/!\1/' \
      -e 's/Fn::('"${(j:|:)custom_fns}"'):/!\1/' \
      -e 's/<<Include:/<<: !Include/' \
    > "$OUTPUT"

  if ! (( OPT_test )); then
    git add "$OUTPUT"
  fi
}

get_changed_dhall

if (( #changed_dhall )); then
  for f in "${(@)changed_dhall}"; do
    convert_dhall "$f"
  done
fi
